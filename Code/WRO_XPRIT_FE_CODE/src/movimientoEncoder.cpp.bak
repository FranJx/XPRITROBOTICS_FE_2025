#include <Arduino.h>
#include <Wire.h>
#include <Servo.h>
#include <Adafruit_Sensor.h>
#include <Adafruit_BNO055.h>
#include <VL53L0X.h>

int orientation = 0; // 0=North, 90=East, 180=South, 270=West
int facing = 0;



// Pines del motor y encoder
#define MA1 5
#define MA2 4
#define PWM 6
#define ENCODER_A 3

// Servo y BNO055
#define SERVO_PIN 9

// Pines XSHUT
#define XSHUT1 A3
#define XSHUT2 A6

// Direcciones I2C nuevas
#define VL1_ADDR 0x30
#define VL2_ADDR 0x31
#define VL3_ADDR 0x32

// Parámetros del encoder y rueda
const float WHEEL_DIAMETER_MM = 35.0;
const float WHEEL_PERIMETER_MM = WHEEL_DIAMETER_MM * 3.1416;
const int ENCODER_PULSES_PER_REV = 420;
const float PULSES_PER_MM = ENCODER_PULSES_PER_REV / WHEEL_PERIMETER_MM;
const int DIST_MM = 450; // 10 cm
const int TARGET_PULSES = PULSES_PER_MM * DIST_MM;

// PID parámetros
float Kp = 0.9;   // Proporcional
float Ki = 0.0;   // Integral
float Kd = 1.0;   // Derivativo

volatile long encoderCount = 0;
Servo direccion;
Adafruit_BNO055 bno = Adafruit_BNO055(55);

VL53L0X vl1;
VL53L0X vl2;
VL53L0X vl3;

int distVL1 = 0;
int distVL2 = 0;
int distVL3 = 0;

void encoderISR() {
  encoderCount++;
}

void setup() {
  Serial.begin(115200);
  pinMode(8, OUTPUT); // buzzer
  pinMode(MA1, OUTPUT);
  pinMode(MA2, OUTPUT);
  pinMode(PWM, OUTPUT);
  pinMode(ENCODER_A, INPUT_PULLUP);

  direccion.attach(SERVO_PIN);
  direccion.write(90); // Recto

  attachInterrupt(digitalPinToInterrupt(ENCODER_A), encoderISR, RISING);

  if (!bno.begin()) {
    Serial.println("No se detecta BNO055");
    while (1);
  }
  delay(1000);
  bno.setExtCrystalUse(true);

  Serial.println("Listo para control PID de direccion y motor");

  pinMode(XSHUT1, OUTPUT);
  pinMode(XSHUT2, OUTPUT);

  // Apaga ambos sensores con XSHUT
  digitalWrite(XSHUT1, LOW);
  digitalWrite(XSHUT2, LOW);
  delay(10);

  Wire.begin();

  // Inicializa VL1
  digitalWrite(XSHUT1, HIGH);
  delay(10);
  if (!vl1.init(true)) {
    Serial.println("Fallo al inicializar VL1");
  }
  vl1.setAddress(VL1_ADDR);
  Serial.println("VL1 direccionado correctamente");

  // Inicializa VL2
  digitalWrite(XSHUT2, HIGH);
  delay(10);
  if (!vl2.init(true)) {
    Serial.println("Fallo al inicializar VL2");
  }
  vl2.setAddress(VL2_ADDR);
  Serial.println("VL2 direccionado correctamente");

  // Inicializa VL3 (sin XSHUT, ya debe estar encendido)
  if (!vl3.init(true)) {
    Serial.println("Fallo al inicializar VL3");
  }
  vl3.setAddress(VL3_ADDR);
  Serial.println("VL3 direccionado correctamente");

  vl1.startContinuous();
  vl2.startContinuous();
  vl3.startContinuous();
  delay(200);
  tone(8, 1500, 50);
  delay(70);
  tone(8, 1500, 50);
  delay(70);
}

void moveMotorPID(bool forward, int pulses) {
  encoderCount = 0;

  // Inicializa motor
  if (forward) {
    digitalWrite(MA1, HIGH);
    digitalWrite(MA2, LOW);
  } else {
    digitalWrite(MA1, LOW);
    digitalWrite(MA2, HIGH);
  }

  int maxPWM = 200;
  int minPWM = 80;

  // PID variables
  float setpoint = 0;
  float last_error = 0;
  float integral = 0;

  // Lee el heading inicial
  sensors_event_t event;
  bno.getEvent(&event);
  setpoint = 0 + facing; // Heading absoluto
  if (forward == false) {
    setpoint += 180;
    if (setpoint > 360) setpoint -= 360;
  }

  while (encoderCount < pulses) {
    // Lee heading actual
    bno.getEvent(&event);
    float heading = event.orientation.x;

    // Calcula error de heading (considera wrap-around 0-360)
    float error = heading - setpoint;
    if (error > 180) error -= 360;
    if (error < -180) error += 360;

    integral += error;
    float derivative = error - last_error;

    float correction = Kp * error + Ki * integral + Kd * derivative;

    // Corrige servo (90 es recto)
    int servo_angle = 85 - correction;
    if (servo_angle < 50) servo_angle = 50;
    if (servo_angle > 150) servo_angle = 150;
    direccion.write(servo_angle);

    // Control de velocidad proporcional a la distancia restante
    int error_dist = pulses - encoderCount;
    int pwmValue = 0.5 * error_dist;
    if (pwmValue > maxPWM) pwmValue = maxPWM;
    if (pwmValue < minPWM) pwmValue = minPWM;
    analogWrite(PWM, pwmValue);

    // Leer sensores VL53L0X y mostrar por Serial
    distVL1 = vl1.readRangeContinuousMillimeters();
    distVL2 = vl2.readRangeContinuousMillimeters();
    distVL3 = vl3.readRangeContinuousMillimeters();

    Serial.print("Pulsos: "); Serial.print(encoderCount);
    Serial.print(" | Heading: "); Serial.print(heading);
    Serial.print(" | Servo: "); Serial.print(servo_angle);
    Serial.print(" | PWM: "); Serial.print(pwmValue);
    Serial.print(" | VL1: "); Serial.print(distVL1); Serial.print(" mm");
    Serial.print(" | VL2: "); Serial.print(distVL2); Serial.print(" mm");
    Serial.print(" | VL3: "); Serial.print(distVL3); Serial.println(" mm");

    last_error = error;
    delay(10);
  }

  // Detener motor y centrar servo
  analogWrite(PWM, 0);
  digitalWrite(MA1, LOW);
  digitalWrite(MA2, LOW);
  direccion.write(90);
}

void loop() {
  Serial.println("Moviendo adelante 10cm...");
  moveMotorPID(true, TARGET_PULSES);
  orientation = orientation + 1;
  if (orientation >= 4) orientation = 0;
  facing = orientation * 90;

  delay(500); // Pequeña pausa antes de repetir
}